HubotでURLが貼られたらページのタイトルをしゃべるようにする CoffeeScript, hubot
=====
<a href="http://manaten.net/wp-content/uploads/2013/12/hubot.png"><img src="http://manaten.net/wp-content/uploads/2013/12/hubot.png" alt="hubot" width="418" height="122" class="aligncenter size-full wp-image-860" /></a>

よくあるやつ。

```javascript
request = require 'request'
cheerio = require 'cheerio'

module.exports = (robot) ->
  robot.hear /(h?ttps?:\/\/[-a-zA-Z0-9@:%_\+.~#?&\/=]+)/i, (msg)->
    request { uri: msg.match[1] }, (error, response, body)->
      return if error
      $ = cheerio.load body.replace(/<!\[CDATA\[([^\]]+)]\]>/ig, "$1")
      title = $("title")
      robot.adapter.notice msg.envelope, "#{title.text()}" if title
```
[github](https://github.com/manaten/mana_bot/blob/master/scripts/url_title.coffee)

URLにマッチしたら、それをそのままrequestして、取得したbodyをcheerioでパースし、タイトルタグの内容を喋らせるだけ。かんたん。

## 追記
URL先の文字コードがUTF-8以外だと文字化けするので、iconv使ってこんな風にした。

```javascript
request = require 'request'
cheerio = require 'cheerio'
iconv = require 'iconv'

convertEncode = (body) ->
  charset = body.toString('ascii').match /<meta[^>]*charset\s*=\s*["']?([-\w]+)["']?/i
  return new iconv.Iconv(charset[1], 'UTF-8//TRANSLIT//IGNORE').convert(body) if charset
  body

module.exports = (robot) ->
  robot.hear /(h?ttps?:\/\/[-\w@:%\+.~#?&\/=]+)/i, (msg)->
    uri = msg.match[1]
    request { uri: uri, encoding: null }, (error, response, body)->
      return if error

      $ = cheerio.load convertEncode(body).toString().replace(/<!\[CDATA\[([^\]]+)]\]>/ig, "$1")
      title = $("title")
      robot.adapter.notice msg.envelope, "#{title.text()}" if title
```
[github](https://github.com/manaten/mana_bot/blob/master/scripts/url_title.coffee)

UTF-8以外のWebサイトなんていい加減存在しないかと思いきや、大手ニュースサイトがそうだったりするので大変困る。


## 追記2 (12/20)
titleタグが改行含んでたりすると、うまく喋ってくれなかったのでしゃべるところを以下のようにしてみた。

```javascript
if title
  titleText = title.text().replace(/^[\s\n]+/, '').replace(/[\s\n]+$/, '')
  robot.adapter.notice msg.envelope, "#{titleText}"
```
[github](https://github.com/manaten/mana_bot/blob/master/scripts/url_title.coffee)
